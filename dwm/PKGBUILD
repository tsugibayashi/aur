# Maintainer: Takashi Sugibayashi <sugibayashi protonmail com>

pkgname=dwm
pkgver=6.3
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="https://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'st' 'dmenu')
source=(https://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
        https://dwm.suckless.org/patches/systray/dwm-systray-$pkgver.diff
        dwm.desktop
        config.h)
sha256sums=('badaa028529b1fba1fd7f9a84f3b64f31190466c858011b53e2f7b70c6a3078d'
            'b2d5914e26eb5dbe1a4649a213884544d841f6a5f157e93eca8f7f444b749fde'
            'bc36426772e1471d6dd8c8aed91f288e16949e3463a9933fee6390ee0ccd3f81'
            'SKIP')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 < ../dwm-systray-$pkgver.diff
  if [ -e "$srcdir/config.h" ]; then
      cp "$srcdir/config.h" config.h
  fi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm644 "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}
