# Maintainer: Takashi Sugibayashi <sugibayashi protonmail com>

pkgname=dwm
pkgver=6.6
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="https://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'dmenu')
source=(https://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
        dwm-bottomgaps-6.4.diff
        dwm.desktop
        config.h)
sha256sums=('7cfc2c6d9386c07c49e2c906f209c18ba3364ce0b4872eae39f56efdb7fc00a3'
            '9044fecc66bd60b4004a304155915603f961d0c9e95bc85d82547def89742467'
            'bc36426772e1471d6dd8c8aed91f288e16949e3463a9933fee6390ee0ccd3f81'
            'SKIP')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  #patch -p1 < ../dwm-systray-$pkgver.diff
  patch -p1 < ../dwm-bottomgaps-6.4.diff

  if [ -e "$srcdir/config.h" ]; then
      cp "$srcdir/config.h" config.h
  fi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm644 "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}
