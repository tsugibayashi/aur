# Original Maintainer: ALX99 <46844683+ALX99@users.noreply.github.com>
# Maintainer: Takashi Sugibayashi <sugibayashi protonmail com>

pkgname=river-creek
_pkgname=creek
pkgver=0.4.2
pkgrel=2
pkgdesc='A malleable and minimalist status bar for the River compositor'
arch=('x86_64')
url='https://github.com/nmeum/creek'
license=('MIT')
depends=('wayland' 'river')
makedepends=('zig>=0.13.0' 'zig<0.14.0' 'git' 'wayland-protocols' 'pixman' 'fcft')
provides=('creek')
conflicts=('river-creek-git')
source=("https://github.com/nmeum/$_pkgname/archive/refs/tags/v$pkgver.tar.gz"
        change_tags_number.patch)
sha256sums=('bb9cffb0c5980524e5338cea0c11f87dcf3813a90626a69e504665961fb13401'
            'de978a25200777741eb816decd4b4bf26db9dce77ab5a1dde6f2791ab4032811')

build() {
  cd "$_pkgname-$pkgver"
  patch -p1 < ../change_tags_number.patch
  zig build --release=safe
}

check() {
  cd "$_pkgname-$pkgver"
  test -r zig-out/bin/creek
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  DESTDIR="$pkgdir" zig build --release=safe --prefix '/usr'
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
