# Maintainer: Takashi Sugibayashi <sugibayashi protonmail com>

pkgname=st
pkgver=0.9.2
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
license=('MIT')
depends=(libxft)
url=https://st.suckless.org
source=(https://dl.suckless.org/$pkgname/$pkgname-$pkgver.tar.gz
        terminfo.patch
        https://st.suckless.org/patches/disable_bold_italic_fonts/st-disable-bold-italic-fonts-0.8.2.diff
        config.h)
sha256sums=('6b215d4f472b21d6232f30f221117a777e24bcfee68955ddefb7426467f9494b'
            'f9deea445a5c6203a0e8e699f3c3b55e27275f17fb408562c4dd5d649edeea23'
            '6da0bbfe66f76d06428713ddac325894dbd5a15ad626a42b4161c2d4fcaa48fe'
            'SKIP')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p0 < ../terminfo.patch
  patch -p1 < ../st-disable-bold-italic-fonts-0.8.2.diff

  if [ -e "$srcdir/config.h" ]; then
    cp -p "$srcdir/config.h" config.h
  fi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm644 st.info "$pkgdir/usr/share/st.info"
}

